! version = 2.0

// Variablen
! var botmaster = Gui und Gab

// testing
+ [*](@potentialmed)[*]
- Das ist ein Medikament.

// Antwortmuster

+ [*] (@medication) [*] nicht [*]
- Du konntest {uppercase}<star>{/uppercase} nicht einnehmen? {topic=medication} <set talkingmed={uppercase}<star>{/uppercase}>

+ [*] (@medication) [*]
- Was möchtest du über {uppercase}<star>{/uppercase} wissen? {topic=medication} <set talkingmed={uppercase}<star>{/uppercase}>

> topic medication
  // when the patient mentioned a known medication
  // (the mentioned medication is saved as the variable talkingmed)

  + *
  - Hast du noch weitere Fragen zu <get talkingmed>?

  + (@medication)
  * <get talkingmed> != {uppercase}<star>{/uppercase} => <set talkingmed={uppercase}<star>{/uppercase}><get talkingmed> steht auch auf deinem Medikationsplan. Was möchtest du dazu wissen?
  - {@ *}

  + nein
  % hast du noch weitere fragen *
  - OK. Du kannst mich alles zu deiner Medikation fragen. {topic=random}

  + (abbrechen|stop|vergiss es)
  @ nein

  + nein
  % * nicht einnehmen
  - Warum konntest du <get talkingmed> nicht einnehmen?

  + (ja|doch)
  % * nicht einnehmen
  - Soll ich dich zum Medikationstagebuch weiterleiten?

  + (ja|gerne|bitte)
  % soll ich dich zum medikationstagebuch weiterleiten
  - <bot compliance>

  + [*] (wann|zeit|uhr) [*]
  - inst#medWhen#<get talkingmed>#Du solltest <get talkingmed> zu folgenden Zeiten einnehmen:\n
  // TODO: can we do a redirect to {@ *} after some waiting time?

  + [*] (vor|nach) [*] dem essen [*]
  @ wie einnehmen

  + [*] wie [*] (einnehmen|einnahme|nehmen|anwenden)
  - inst#medHow#<get talkingmed>#Du solltest <get talkingmed>

  + [*] (warum|wieso|grund) [*]
  - inst#medWhy#<get talkingmed>#Als Grund für die Einnahme von <get talkingmed> ist folgendes eingetragen:

  + [*] (lange|dauer) [*]
  - inst#medDuration#<get talkingmed>#Du solltest <get talkingmed> in folgendem Zeitraum einnehmen:

  + [*] (wirkung|nebenwirkung|nebenwirkungen) [*]
  - Ich habe keine Nebenwirkungen gespeichert. Soll ich für dich im Compendium nachschlagen?

  + [*] (ja|gerne|bitte) [*]
  % * compendium nachschlagen
  - inst#compendium#<get talkingmed>#Bitte sehr, ich schlage <get talkingmed> im Compendium nach.

< topic

> topic doctor
+ *
- Reden wir über etwas anderes. Was kann ich für dich tun? {topic=random}

+ [*](nein|keinen)[*]
* <get doctor> == undefined => Es wäre gut, einen kompetenten Hausarzt zu haben. {topic=random}<set doctor=none>
- OK.{topic=random}

+ [*](ja|habe)[*]
% (hast du eigentlich einen hausarzt|hast du dir einen hausarzt gesucht)
- Wie heisst er denn?

+ [*] dr *
% wie heisst er denn
- <set doctor=Dr. {formal}<star>{/formal}> Ah, <get doctor> ist nett.{topic=random}

+ [*] heisst *
% wie heisst er denn
<set doctor={formal}<star>{/formal}>Ich glaube, bei <get doctor> bist du in guten Händen.{topic=random}

< topic

+ [*] (@potentialmed) [*]
- <set talkingmed>={uppercase}<star>{/uppercase}Nimmst <get talkingmed> ein?

+ [*] ja [*]
% nimmst du * ein
- Soll ich <get talkingmed> in deine Medikationsliste aufnehmen? {topic=newmed}

> topic newmed

< topic

+ was kannst du *
- Ich kann unter anderem deine Medikamente anzeigen. {weight=3}
- Ich kann unter anderem neue Medikamente einlesen. {weight=2}
- Ich kann dich unter anderem an deine Medikamente erinnern. {weight=4}
- Ich kann dir mit deiner Medikation helfen {weight=10}
- Ich kann so einiges.

+ ?
- <bot informationquestion>
- Hast du alle Medikamente schon erfasst? {weight=10}
- Du kannst zum Beispiel eingenommene Medikamente erfassen, neue Medikamente hinzufügen oder auch einfach mit mir plaudern.
- Du kannst mir Fragen zu deinen Medikamenten stellen, zum Beispiel wann du sie einnehmen musst.
- Du kannst sämtliche Funktionen auch über das Menü oben links erreichen.

+ was kann ich [*]
@ ?

+ wie [*] weiter
@ ?

+ wie * helfen
@ ?

+ hilfe
- Du weisst nicht was du hier machen kannst? {@?}

+ ja
% hast du alle medikamente schon erfasst
- Dann kannst du mir zum Beispiel sagen wann du sie eingenommen hast. Ich führe dein Medikationstagebuch.

+ ich habe *
% dann kannst du mir zum beispiel sagen wann du sie eingenommen hast ich führe dein medikationstagebuch
- Das Medikament kenne ich noch nicht, möchtest du es erfassen?

+ * (@medication) *
% dann kannst du mir zum beispiel sagen wann du sie eingenommen hast ich führe dein medikationstagebuch
- Achso, du hast <star> genommen?

+ [*] nein [*]
% hast du alle medikamente schon erfasst
- Möchtest du jetzt ein weiteres Medikament hinzufügen?

+ [*] ja [*]
% möchtest du jetzt ein weiteres medikament hinzufügen
- <bot medicationview>

+ [*] ja [*]
% das medikament kenne ich noch nicht möchtest du es erfassen
- <bot medicationview>

+ [*] nein [*]
% das medikament kenne ich noch nicht möchtest du es erfassen
- Alles klar, dann nicht.

+ was soll ich *
- Du musst gar nichts.

+ das habe ich leider nicht verstandend
- Ich habe früher auch vieles nicht verstanden. Aber ich habe dazu gelernt.

+ [*] medikamente [*] (ich|nehmen|habe) [*]
- Soll ich deine Medikamentenliste anzeigen?

+ [*](ja|bitte|gerne)[*]
% soll ich deine medikamentenliste anzeigen
@ medikamente

+ [*](nein)[*]
% soll ich deine medikamentenliste anzeigen
- Über welches Medikament möchtest du etwas wissen?

+ [*] meine [*] (medi|medikation|medikamente)
@ medikamente

+ [*] (ernährungsplan|ernährung) [*]
- Möchtest du wissen, was du mit deinen Medikamenten bei Lebensmitteln beachten solltest?

+ [*](ja|bitte|gerne)[*]
% möchtest du wissen was du mit deinen medikamenten bei lebensmitteln beachten solltest
- <bot nutrition>

// these questions where hardcoded in the original eMMA 1.0

+ [*] über [*] (dich|emma) [*]
- <bot about>

+ was kann ich (einnehmen|essen)
- <bot nutrition>

+ medikamente
- <bot medicationview>

+ [*] (auswertung|statistik|tagebuch) [*] medikamente [*]
- <bot compliance>

+ [*] welche medikamente [*] eingenommen [*]
- <bot compliance>

+ (ok|super|danke|toll)
- Es freut mich, dass ich dir helfen konnte.

+ delete
- <bot deleteStorage>

+ reminder nacht
- <bot reminderNight>

+ reminder morgen
- <bot reminderMorning>

+ reminder mittag
- <bot reminderMidday>

+ reminder abend
- <bot reminderEvening>

+ [*](ich heisse|nenne mich wieder|nenne mich|mein name ist) *
- inst#name#{formal}<star2>{/formal}#OK, ich werde dich <set name={formal}<star2>{/formal}><get name> nennen.

+ (ändere|wechsle) meinen namen [*]
- Wie soll ich dich denn nennen?

+ *
% wie soll ich dich denn nennen
- inst#name#{formal}<star>{/formal}#OK, ich nenne dich ab sofort <set name={formal}<star>{/formal}><get name>.

+ (ich heisse|nenne mich) *
% wie soll ich dich denn nennen
- inst#name#{formal}<star2>{/formal}#Alles klar, ich nenne dich ab sofort <set name={formal}<star2>{/formal}><get name>.
