! version = 2.0

// Variablen
! var botmaster = Gui und Gab

// Antwortmuster

+ [*] (@medication) [*] nicht [*]
- Du konntest {uppercase}<star>{/uppercase} nicht einnehmen? {topic=medication} <set talkingmed={uppercase}<star>{/uppercase}>

+ [*] (@medication) [*]
- Was möchtest du über {uppercase}<star>{/uppercase} wissen? {topic=medication} <set talkingmed={uppercase}<star>{/uppercase}>

> topic medication
  // when the patient mentioned a known medication
  // (the mentioned medication is saved as the variable talkingmed)

  + *
  - Hast du noch weitere Fragen zu <get talkingmed>?

  + nein
  % hast du noch weitere fragen *
  - OK. Du kannst mich alles zu deiner Medikation fragen. {topic=random}

  + nein
  % * nicht einnehmen
  - Warum konntest du <get talkingmed> nicht einnehmen?

  + (ja|doch)
  % * nicht einnehmen
  - Soll ich dich zum Medikationstagebuch weiterleiten?

  + (ja|gerne|bitte|)
  % soll ich dich zum medikationstagebuch weiterleiten
  - <bot compliance>

  + [*] wann [*]
  % was möchtest du über * wissen
  - #inst##<get talkingmed>##Du solltest <get talkingmed> zu folgenden Zeiten einnehmen:

< topic

+ was kannst du *
- Ich kann unter anderem deine Medikamente anzeigen. {weight=2}
- Ich kann unter anderem neue Medikamente einlesen. {weight=2}
- Ich kann dich unter anderem an deine Medikamente erinnern. {weight=2}
- Ich kann so einiges.

+ ?
- <bot informationquestion>
- Hast du alle Medikamente schon erfasst? {weight=10}
- Du kannst zum Beispiel eingenommene Medikamente erfassen, neue Medikamente hinzufügen oder auch einfach mit mir plaudern.
- Du kannst mir Fragen zu deinen Medikamenten stellen, zum Beispiel wann du sie einnehmen musst.
- Du kannst sämtliche Funktionen auch über das Menü oben links erreichen.

+ was kann ich [*]
@ ?

+ wie [*] weiter
@ ?

+ wie * helfen
@ ?

+ hilfe
- Du weisst nicht was du hier machen kannst? {@?}

+ ja
% hast du alle medikamente schon erfasst
- Dann kannst du mir zum Beispiel sagen wann du sie eingenommen hast. Ich führe dein Medikationstagebuch.

+ ich habe *
% dann kannst du mir zum beispiel sagen wann du sie eingenommen hast ich führe dein medikationstagebuch
- Das Medikament kenne ich noch nicht, möchtest du es erfassen?

+ * (@medication) *
% dann kannst du mir zum beispiel sagen wann du sie eingenommen hast ich führe dein medikationstagebuch
- Achso, du hast <star> genommen?

+ [*] nein [*]
% hast du alle medikamente schon erfasst
- Möchtest du jetzt ein weiteres Medikament hinzufügen?

+ [*] ja [*]
% möchtest du jetzt ein weiteres medikament hinzufügen
- <bot medicationview>

+ [*] ja [*]
% das medikament kenne ich noch nicht möchtest du es erfassen
- <bot medicationview>

+ [*] nein [*]
% das medikament kenne ich noch nicht möchtest du es erfassen
- Alles klar, dann nicht.

+ was soll ich *
- Du musst gar nichts.

+ das habe ich leider nicht verstandend
- Ich habe früher auch vieles nicht verstanden. Aber ich habe dazu gelernt.

// these questions where hardcoded in the original eMMA 1.0
+ was kann ich (einnehmen|essen)
- <bot nutrition>

+ medikamente
- <bot medicationview>

+ [*] meine [*] (medi|medikation|medikamente)
@ medikamente

+ delete
- <bot deleteStorage>

+ reminder nacht
- <bot reminderNight>

+ reminder morgen
- <bot reminderMorning>

+ reminder mittag
- <bot reminderMidday>

+ reminder abend
- <bot reminderEvening>

+ [*](ich heisse|nenne mich wieder|nenne mich|mein name ist) *
- inst#name#{formal}<star2>{/formal}#OK, ich werde Dich <set name={formal}<star2>{/formal}><get name> nennen.
